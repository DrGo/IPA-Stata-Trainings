{smcl}
{txt}{...}
{pstd}{c TLC}{hline 43}{c TRC}{break}
{bf}{...}
{c |}{view `""Post Kenya 2014 - Stata 102.smcl""': Innovations for Poverty Action{space 12}}{c |}{break}
{c |}{view `""Post Kenya 2014 - Stata 102.smcl""': The Abdul Latif Jameel Poverty Action Lab{space 1}}{c |}{break}
{c |}{view `""Post Kenya 2014 - Stata 102.smcl""':{space 43}}{c |}{break}
{c |}{view `""Post Kenya 2014 - Stata 102.smcl""': Staff Training - Kenya 2014 {space 14}}{c |}{break}
{c |}{view `""Post Kenya 2014 - Stata 102.smcl""': Stata 102{space 33}}{c |}{break}
{sf}{...}
{c BLC}{hline 43}{c BRC}

{pstd}{bf:Variable Properties}{break}

{pstd}{bf:{stata `"use "Raw/Post Kenya 2014 - Stata 102", clear"'}}{p_end}

{marker stuffofstata}{...}
{pstd}Variables are the stuff of Stata.
After all, a Stata dataset is just a collection of variables.
Whether you're manipulating the data,
calculating summary statistics,
or running a regression,
you're working with variables.

{pstd}To some extent, variables are just a set of values.
However, they're also characterized by a series of properties:
a {help varname:name},
{help label:variable and value labels},
a {help data_types:storage type} (numeric or string),
and a {help format:display format}.
These properties are essential information about a variable,
and you usually want to get them right before moving on
to more advanced data work.
For example, it doesn't make sense to use a variable throughout a do-file
only to rename it at the end.
Thus, attending to variable properties is typically the first task in the data cleaning process.

{pstd}This module is divided into sections for these variable properties:

{pstd}{view `""SMCL/Tasks/Variable Properties.smcl"##names"':1. Variable Names}{break}
{view `""SMCL/Tasks/Variable Properties.smcl"##labels"':2. Labels}{break}
{view `""SMCL/Tasks/Variable Properties.smcl"##types"':3. Storage Types}

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} {bf:Display formats}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} In addition to its name, labels, and storage type, a variable is}{c |}{p_end}
{p2col:{c |} characterized by a {help format:display format} that determines how it is displayed in the}{c |}{p_end}
{p2col:{c |} absence of a value label. This module doesn't cover formats in depth, but}{c |}{p_end}
{p2col:{c |} below is a short example. The following code first formats variable}{c |}{p_end}
{p2col:{c |} {cmd:gear_ratio} in the auto dataset so that it is displayed with exactly one}{c |}{p_end}
{p2col:{c |} digit after the decimal, then with exactly three:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata sysuse auto, clear}}}{c |}{p_end}
{p2col:{c |} {bf:{stata format gear_ratio %9.1f}}}{c |}{p_end}
{p2col:{c |} {bf:{stata list gear_ratio}}}{c |}{p_end}
{p2col:{c |} {bf:{stata format gear_ratio %9.3f}}}{c |}{p_end}
{p2col:{c |} {bf:{stata list gear_ratio}}}{c |}{p_end}
{p2col:{c |} {bf:{stata use "Raw/Post Kenya 2014 - Stata 102", clear}}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{hline}{marker names}

{pstd}{bf:1. Variable Names}

{hline}

{pstd}You probably already know {helpb rename}.
Another command for renaming variables is {helpb renpfix},
which adds, changes, or removes prefixes on variable names.

{pstd}This dataset is from the baseline,
and if we'd like to merge it with the endline data,
we might first want to add a prefix like {cmd:bl_} to all variables:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
renpfix "" bl_
{txt}{sf}{ul off}{...}

{pstd}This changes the prefix from {cmd:""} (in other words, no prefix) to {cmd:bl_}.

{pstd}There's one variable that shouldn't have been renamed:

{pstd}{bf:{stata `"rename bl_hhid hhid"'}}{p_end}

{pstd}This is because {cmd:hhid} would be used to merge the two datasets.

{pstd}Using {cmd:renpfix}, we can change the prefix:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
renpfix bl_ baseline
{txt}{sf}{ul off}{...}

{pstd}As before,
first specified is the prefix to change {it:from} (blank or nonblank),
followed by the prefix to change {it:to}.
The second prefix can also be blank.
For example, the following command removes the prefix:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
renpfix baseline
{txt}{sf}{ul off}{...}

{pstd}The new {helpb rename group:rename} introduced in Stata 12
is a big improvement over the previous version,
and offers many options for renaming groups of variables.
If you have Stata 12 or higher, you can use {cmd:rename} instead of {cmd:renpfix}.
For example, this adds the prefix {cmd:bl_} to all variables:

{pstd}{bf:{stata `"rename * bl_*"'}}{p_end}

{pstd}Again in Stata 12 and higher, this changes all variable names to lowercase:

{pstd}{bf:{stata `"rename *, lower"'}}{p_end}

{marker renvars}{...}
{pstd}If you don't have Stata 12 or higher,
but {cmd:rename}'s extended functionality appeals to you,
{cmd:renvars} is a user-written program that provides many of the same options:

{pstd}{bf:{stata `"net search renvars"'}}{p_end}

{pstd}For example, this use of {cmd:renvars} changes all variable names to lowercase:

{pstd}{bf:{stata `"renvars *, lower"'}}{p_end}

{hline}{marker labels}

{pstd}{bf:2. Labels}

{hline}

{pstd}{bf:{stata `"use "Raw/Post Kenya 2014 - Stata 102", clear"'}}{p_end}

{pstd}We will now review {helpb label}.
There are two main types of labels: variable labels and value labels.

{pstd}Variable labels are descriptions of variables,
and are usually more informative than variable names.
Variable {cmd:areadur_unit} ("area duration unit") is the time unit (day/week/etc.)
for variable {cmd:areadur}.
However, while it's clear from its variable label what {cmd:areadur} represents,
{cmd:areadur_unit} is missing a variable label:

{pstd}{bf:{stata `"describe areadur areadur_unit"'}}{p_end}

{pstd}The following command adds a variable label to {cmd:areadur_unit}:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
label variable areadur_unit "How long has your household been living in this area? (unit)"
{txt}{sf}{ul off}{...}

{pstd}{bf:{stata `"describe areadur areadur_unit"'}}{p_end}

{pstd}This added descriptive text to {cmd:areadur_unit}.
A common choice for the variable label is the question text.
However, variable labels are limited to 80 characters,
so some questions may not fit.
In this dataset, the variable label of {cmd:own4wheeleryn} has been truncated:

{pstd}{bf:{stata `"describe own4wheeleryn"'}}{p_end}

{pstd}An alternative is to add question text in {helpb notes},
leaving the variable label free for a shorter description.
Here's how we might do this for {cmd:own4wheeleryn}:

{pstd}{bf:{stata `"notes own4wheeleryn: Over the past 12 months, has anyone in your household had for private use any car, van, tractor, bus, 3 wheeler, or truck?"'}}{p_end}
{pstd}{bf:{stata `"label variable own4wheeleryn "Household member had 4-wheeled vehicle for private use in last 12 months""'}}{p_end}
{pstd}{bf:{stata `"describe own4wheeleryn"'}}{p_end}
{pstd}{bf:{stata `"notes own4wheeleryn"'}}{p_end}

{pstd}While variable labels describe variables as a whole,
value labels describe particular values.
A value label is a list of associations of integers and descriptive text.
After a value label is attached to a variable,
the values of the variable are shown as their associated text, not as numbers,
for example, when you {cmd:browse}.

{pstd}Currently, the values of {cmd:areadur_unit} are not labeled:

{pstd}{bf:{stata `"tabulate areadur_unit"'}}{p_end}

{pstd}This is a problem, because even if {cmd:areadur} equals {cmd:1},
{cmd:1} week is very different from {cmd:1} year.
First we need to define the value label,
declaring the integer to text correspondences:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
label define timeunit 1 days 2 weeks 3 months 4 years
{txt}{sf}{ul off}{...}

{pstd}Then we attach the value label to the variable:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
label values areadur_unit timeunit
{txt}{sf}{ul off}{...}

{pstd}Let's see if it worked:

{pstd}{bf:{stata `"tabulate areadur_unit"'}}{p_end}

{pstd}Much better.

{pstd}Now, while the values of {cmd:areadur_unit} are {it:displayed} differently
in commands like {cmd:tabulate},
the actual values of the variable are unchanged,
and the variable has not been converted to a string variable.
For example, the results of {cmd:summarize} are the same
as before {cmd:areadur_unit} was value labeled:

{pstd}{bf:{stata `"summarize areadur_unit"'}}{p_end}

{pstd}Value labels are just for display,
and they make working with numeric variables easier.
A value labeled numeric variable is typically easier to work with
than a string variable with values equal to the value label text.
For example, it's easier to create dummy variables from numeric variables than string variables.

{marker dummy_sets}{...}
{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} What's the best way to create a set of dummy variables from a variable?}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} First, ask whether it's even necessary to create the dummies: if you're}{c |}{p_end}
{p2col:{c |} using Stata 11 or higher, you can use {help fvvarlist:factor variables}. For example:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata regress thanavisityn i.castecode}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} It's easy to specify interactions with factor variables {hline 2} all without}{c |}{p_end}
{p2col:{c |} creating a single dummy variable:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata regress thanavisityn castecode#sex}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} If you don't have Stata 11 or higher, {helpb xi} can complete some of the same tasks}{c |}{p_end}
{p2col:{c |} as factor variables. Even if you have Stata 11 or higher, some commands}{c |}{p_end}
{p2col:{c |} don't allow factor variables, but do take the {cmd:xi} prefix. {cmd:xi} can also create}{c |}{p_end}
{p2col:{c |} a set of dummy variables for use outside a single command, and can be more}{c |}{p_end}
{p2col:{c |} convenient than the equivalent for factor variables, {helpb fvrevar}. Unlike factor}{c |}{p_end}
{p2col:{c |} variables, {cmd:xi} works with string variables in addition to numeric.}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} There are also great user-written programs for the job. Check out these {help SSC}}{c |}{p_end}
{p2col:{c |} programs:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata ssc install todummy}}}{c |}{p_end}
{p2col:{c |} {bf:{stata ssc install dummieslab}}}{c |}{p_end}
{p2col:{c |} {bf:{stata ssc install dummies2}}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{pstd}We used {cmd:timeunit} for the name of the value label that we attached to {cmd:areadur_unit}.
The name of the value label was different from the variable name,
but Stata also would have allowed it to be the same.

{pstd}You can modify value labels:

{pstd}{bf:{stata `"label define timeunit 3 "lunar cycles", modify"'}}{p_end}
{pstd}{bf:{stata `"label define timeunit 5 decades, add"'}}{p_end}
{pstd}{bf:{stata `"label list timeunit"'}}{p_end}

{pstd}Further, the same value label can be attached to multiple variables.
The following command attaches the value label {cmd:timeunit}
to the variable {cmd:addressdur_unit};
{cmd:timeunit} is already attached to {cmd:areadur_unit}.

{pstd}{bf:{stata `"label values addressdur_unit timeunit"'}}{p_end}

{pstd}{bf:{stata `"browse addressdur_unit areadur_unit"'}}{p_end}

{pstd}It's also possible to remove value labels:

{pstd}{bf:{stata `"label values areadur_unit"'}}{p_end}
{pstd}{bf:{stata `"label values addressdur_unit"'}}{p_end}

{pstd}{bf:{stata `"browse areadur_unit addressdur_unit"'}}{p_end}

{pstd}You can attach value labels to multiple variables at the same time:

{pstd}{bf:{stata `"label values areadur_unit addressdur_unit timeunit"'}}{p_end}

{pstd}{bf:{stata `"browse areadur_unit addressdur_unit"'}}{p_end}

{pstd}Or list all value labels:

{pstd}{bf:{stata `"label dir"'}}{p_end}

{pstd}{bf:{stata `"label list"'}}{p_end}

{pstd}The {helpb #delimit} command sets the character that indicates the end of a command.
By default, it's a carriage return, the character that the enter key makes.
This means that in a do-file, once you press enter and continue to the next line,
the previous command is registered as complete:
the command does not extend to the next line.

{pstd}Alternatively, you can make the delimiter a semicolon.
This means that Stata considers a command to be complete only once it sees a semicolon,
even if new lines have been started.
The following sets the delimiter to a semicolon.
Try it yourself by typing it out in a new do-file, then running it.
You can change the delimiter only within do-files, not interactively in the Command window.

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
#delimit ;{break}
display "Hello world!";{break}
summarize{break}
{p_end}{pmore}sex{break}{p_end}{pstd}
;
{txt}{sf}{ul off}{...}

{pstd}{cmd:#delimit cr} returns to a {ul:c}arriage {ul:r}eturn delimiter:

{pstd}{cmd}#delimit ;{txt}{p_end}

{pstd}{cmd}display{txt}{p_end}

{pmore}{cmd}"Hello world!"{txt}{p_end}

{pmore}{cmd};{txt}{p_end}

{pstd}{cmd}#delimit cr{txt}{p_end}
{pstd}{cmd}display "Hello world!"{txt}{p_end}

{pstd}{stata `"run "Do/Tasks/Variable Properties.do" 1"':Click here to execute.}

{marker delimit}{...}
{pstd}I rarely use {cmd:#delimit ;},
though others prefer the semicolon as a delimiter to the carriage return.
However, {cmd:#delimit ;} is very useful for long value labels.
Compare

{pstd}{bf:{stata `"label define timeunit2 1 milliseconds 2 seconds 3 minutes 4 hours 5 days 6 weeks 7 months 8 quarters 9 trimesters 10 semesters 11 years 12 decades"'}}{p_end}

{pstd}to:

{pstd}{bf:{stata `"label drop timeunit2"'}}{p_end}

{pstd}{cmd}#delimit ;{txt}{p_end}
{pstd}{cmd}label define timeunit2{txt}{p_end}
{pmore}{cmd}1 milliseconds{txt}{p_end}
{pmore}{cmd}2 seconds{txt}{p_end}
{pmore}{cmd}3 minutes{txt}{p_end}
{pmore}{cmd}4 hours{txt}{p_end}
{pmore}{cmd}5 days{txt}{p_end}
{pmore}{cmd}6 weeks{txt}{p_end}
{pmore}{cmd}7 months{txt}{p_end}
{pmore}{cmd}8 quarters{txt}{p_end}
{pmore}{cmd}9 trimesters{txt}{p_end}
{pmore}{cmd}10 semesters{txt}{p_end}
{pmore}{cmd}11 years{txt}{p_end}
{pmore}{cmd}12 decades{txt}{p_end}
{pstd}{cmd};{txt}{p_end}
{pstd}{cmd}#delimit cr{txt}{p_end}

{pstd}{stata `"run "Do/Tasks/Variable Properties.do" 2"':Click here to execute.}

{pstd}The second definition is much more readable.
In a do-file, you might have to scroll across to see all of the first definition.

{pstd}A middle path is to use the {cmd:///} {help comments:line-join indicator} with {cmd:#delimit cr}.
{cmd:///} indicates that the command runs onto the next line:

{pstd}{bf:{stata `"label drop timeunit2"'}}{p_end}

{pstd}{cmd}label define timeunit2 ///{txt}{p_end}
{pmore}{cmd}1 milliseconds ///{txt}{p_end}
{pmore}{cmd}2 seconds ///{txt}{p_end}
{pmore}{cmd}3 minutes ///{txt}{p_end}
{pmore}{cmd}4 hours ///{txt}{p_end}
{pmore}{cmd}5 days ///{txt}{p_end}
{pmore}{cmd}6 weeks ///{txt}{p_end}
{pmore}{cmd}7 months ///{txt}{p_end}
{pmore}{cmd}8 quarters ///{txt}{p_end}
{pmore}{cmd}9 trimesters ///{txt}{p_end}
{pmore}{cmd}10 semesters ///{txt}{p_end}
{pmore}{cmd}11 years ///{txt}{p_end}
{pmore}{cmd}12 decades{txt}{p_end}

{pstd}{stata `"run "Do/Tasks/Variable Properties.do" 3"':Click here to execute.}

{pstd}Like {cmd:#delimit}, the line-join indicator works only within do-files.
Try the following example by typing it out in a new do-file, then running it.

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
tabulate castecode ///{break}
{p_end}{pmore}if sex == 1 & age <= 35, ///{break}{p_end}{pstd}
{p_end}{pmore}missing nolabel{p_end}{pstd}
{txt}{sf}{ul off}{...}

{pstd}Here, the line-join indicator allowed the command to span three lines.
The first line contained the main piece of the command,
{cmd:tabulate castecode}.
The second line then held the {cmd:if} qualifier,
and the third line listed the command's options, {cmd:missing nolabel}.
This command is short enough that it probably could have been put on a single line,
but for longer commands,
it's often most readable to use the line-join indicator in this way
to divide the command at particular points of its syntax.

{pstd}The {help SSC} packages {cmd:labutil} (which includes the useful {cmd:labmask})
and {cmd:labutil2} (which includes {cmd:labrecode})
offer a number of user-written programs
for working with variable and value labels:

{pstd}{bf:{stata `"ssc install labutil"'}}{p_end}
{pstd}{bf:{stata `"ssc install labutil2"'}}{p_end}

{hline}{marker types}

{pstd}{bf:3. Storage Types}

{hline}

{pstd}We will now touch on the rich area of {help data_types:storage types}.
A variable's storage type determines
whether it is numeric or string
and how many significant digits it has.
Storage types include {cmd:byte}, {cmd:float}, and {cmd:str100}.
Storage types are emphatically different from {help format:display formats},
which control how a variable is {it:displayed}.
Display formats include {cmd:%9.0g}, {cmd:%td}, and {cmd:%15s}.

{pstd}You can convert string variables to numeric using {helpb destring}:

{pstd}{bf:{stata `"destring hhid, replace"'}}{p_end}

{pstd}You can also convert numeric variables to string using {helpb tostring}:

{pstd}{bf:{stata `"tostring hhid, replace"'}}{p_end}

{...}
{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} Sometimes {cmd:tostring} will fail to convert a variable to string, delivering the}{c |}{p_end}
{p2col:{c |} warning "cannot be converted reversibly." For example:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata sysuse auto, clear}}}{c |}{p_end}
{p2col:{c |} {bf:{stata tostring gear_ratio, replace}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} The solution to this is not option {cmd:force}, which results in loss of}{c |}{p_end}
{p2col:{c |} information. Instead, use option {cmd:format(%24.0g)}:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata tostring gear_ratio, replace format(%24.0g)}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} Option {cmd:format(%24.0g)} works not just in this case to resolve the issue, but}{c |}{p_end}
{p2col:{c |} in all cases.}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata use "Raw/Post Kenya 2014 - Stata 102", clear}}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{pstd}You can convert a string variable to a value labeled numeric variable
using {helpb encode}:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
encode thanavisitreason, generate(visitreason)
{txt}{sf}{ul off}{...}

{pstd}{bf:{stata `"browse thanavisitreason visitreason"'}}{p_end}

{pstd}You can use a specific value label:

{pstd}{cmd}label define visitreasonlab ///{txt}{p_end}
{pmore}{cmd}1{space 2}"To register a Crime" ///{txt}{p_end}
{pmore}{cmd}2{space 2}"To answer charges filed against you" ///{txt}{p_end}
{pmore}{cmd}3{space 2}"To say hello/to chat" ///{txt}{p_end}
{pmore}{cmd}97 "Refuse to answer" ///{txt}{p_end}
{pmore}{cmd}98 "Other" ///{txt}{p_end}
{pmore}{cmd}99 "Don't Know"{txt}{p_end}

{pstd}{stata `"run "Do/Tasks/Variable Properties.do" 4"':Click here to execute.}

{...}
{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
encode thanavisitreason, generate(visitreason2) label(visitreasonlab)
{txt}{sf}{ul off}{...}

{pstd}{bf:{stata `"browse thanavisitreason visitreason visitreason2 if visitreason != visitreason2"'}}{p_end}

{pstd}You can also convert a value labeled variable
to a string variable that contains the value label text
using {helpb decode}:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
decode visitreason, generate(visitreasonstr)
{txt}{sf}{ul off}{...}

{pstd}{bf:{stata `"browse thanavisitreason visitreason visitreasonstr"'}}{p_end}

{pstd}{cmd:destring} and {cmd:tostring} are inverse operations,
as are {cmd:encode} and {cmd:decode}.

{pstd}If a string variable has all numeric values, convert it to numeric using {cmd:destring}.
If the variable does not have all numeric values,
use {cmd:encode} or use {cmd:destring} with option {cmd:ignore()} or {cmd:force}.

{pstd}If a numeric variable is not value labeled, convert it to string using {cmd:tostring}.
If the variable is value labeled,
you can use {cmd:encode} to retain the value label text or
{cmd:tostring} for just the numeric values.

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} If a variable has the wrong storage type, it might not be "precise" enough}{c |}{p_end}
{p2col:{c |} and can take on slightly incorrect values. For example:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{bf}{...}
{p2col:{c |} {stata generate big = 123456789}}{c |}{p_end}
{p2col:{c |} {stata local big = big[1]}}{c |}{p_end}
{p2col:{c |} {stata display "`big'"}}{c |}{p_end}
{p2col:{c |} {stata drop big}}{c |}{p_end}
{txt}{sf}{ul off}{...}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} Variable {cmd:big} was created as type {cmd:float} when it should have been stored as}{c |}{p_end}
{p2col:{c |} {cmd:long} or {cmd:double}. While {cmd:floats} have 7 digits of accuracy, {cmd:longs} and {cmd:doubles}}{c |}{p_end}
{p2col:{c |} have 9 and 16, respectively. Variable {cmd:big} is 9 digits, so it would be}{c |}{p_end}
{p2col:{c |} precise enough as a {cmd:long}:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{bf}{...}
{p2col:{c |} {stata generate long big = 123456789}}{c |}{p_end}
{p2col:{c |} {stata local big = big[1]}}{c |}{p_end}
{p2col:{c |} {stata display "`big'"}}{c |}{p_end}
{p2col:{c |} {stata drop big}}{c |}{p_end}
{txt}{sf}{ul off}{...}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} Click {help data_types:here} for more on precision. There's also a great {browse "http://blog.stata.com/2012/04/02/the-penultimate-guide-to-precision/":Stata blog post} on}{c |}{p_end}
{p2col:{c |} the subject.}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}


{hline}

{pstd}Problem Set: {view `""SMCL/Problem sets/Tasks/Variable Properties - problem set.smcl""':Variable Properties}{break}

{pstd}Next: {view `""SMCL/Tasks/Unique IDs and Duplicates.smcl""':Unique IDs and Duplicates}{break}
Previous: {view `""SMCL/Introduction/Training Introduction.smcl""':Training Introduction}{break}
{view `""Post Kenya 2014 - Stata 102.smcl""':Stata 102 Start}{break}
